# Copyright (C) 2017 Embecosm Limited.

# Contributor Jeremy Bennett <jeremy.bennett@embecosm.com>
# Contributor Ian Bolton <ian.bolton@embecosm.com>

# This file provides supplementary GDB stub procs for RISC-V testing

# This program is free software; you can redistribute it and/or modify it
# under the terms of the GNU General Public License as published by the Free
# Software Foundation; either version 3 of the License, or (at your option)
# any later version.

# This program is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
# FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for
# more details.

# You should have received a copy of the GNU General Public License along
# with this program.  If not, see <http://www.gnu.org/licenses/>.

# The standard GDB stub procs assume that they are using breakpoints rather
# than hardware breakpoints, and are also not fully up to date with some of
# the possible GDB response messages to be matched. The versions here fix
# those problems.


# Note our usage in the log file.
send_user "Using [pwd]/gdb-stub-extra.exp for supplementary proc definitions\n"

# Create the global array if it doesn't exist.
if {! [array exists gdb_stub_info] } {
    global gdb_stub_info
    set gdb_stub_info(ID) 0
}

proc gdb_stub_init { dest args } {
    global gdb_prompt
    global GDB
    global tool_root_dir

    set GDB "riscv32-unknown-elf-gdb"

    if {![info exists GDB]} then {
        set GDB "[lookfor_file ${tool_root_dir} riscv32-unknown-elf-gdb]"
        if { $GDB == "" } {
            set GDB [transform gdb]
        }
    }

    if {[board_info $dest exists gdb_prompt]} {
        set gdb_prompt [board_info $dest gdb_prompt]
    } else {
        set gdb_prompt "\\(gdb\\)"
    }

    return 1
}

proc stacktrace {} {
    set stack "Stack trace:\n"
    for {set i 1} {$i < [info level]} {incr i} {
        set lvl [info level -$i]
        set pname [lindex $lvl 0]
        append stack [string repeat " " $i]$pname
        foreach value [lrange $lvl 1 end] arg [info args $pname] {
            if {$arg ne ""} {
                if {$value eq ""} {
                    info default $pname $arg value
                }
                if {$value eq ""} {
                    set value "{}"
                }
                append stack " $arg='$value'"
            }
        }
        append stack \n
    }
    return $stack
}

# Override standard function to deal with extra possibility for message.
#
# The exec_file function in GDB may respond asking if you wish to change the
# file, rather than kill the program.
#
# gdb_stub_ld -- load PROG into the board
#             Returns a 0 if there was an error,
#                       1 if it loaded successfully.
#
proc gdb_stub_ld { dest prog } {
    global gdb_prompt
    global GDB

    verbose -log "\n\n Prog name is $prog \n\n"
    verbose -log "\n\nST [stacktrace] ST \n\n"


    if {![board_info $dest exists gdb_is_running]} {
	if {![gdb_stub_restart $dest]} {
	    return 0
	}
    }

    set loadfile [file tail $prog]
    set loadpath [file dirname $prog]

    exp_internal 1
    remote_send host "file $prog\n"
    remote_expect host 30 {
	-re "A program is being debug.*Kill it.*y or n. $" {
	    remote_send host "y\n"
	    exp_continue
	}
	# This one isn't in the standard version.
	-re "A program is being debug.*change the file.*y or n. $" {
	    verbose -log "\n\nGRM$expect_out(buffer)GRM\n\n"
            remote_send host "y\n"
	    exp_continue
	}
	-re "BFD:.*invalid string offset.*done.*$gdb_prompt $" {
	    warning "GDB file looks invalid, lets try again"
            exp_internal 0
	    return [gdb_stub_retry_ld "$dest" "$prog"]
	}
        -re "Load new symbol table.*y or n. $" {
	    remote_send host "y\n"
	    exp_continue
	}
	-re "Reading symbols from.*done..*$gdb_prompt $" {}
	-re "$gdb_prompt $" {
	    # Hmmm...is retrying going to help? I kinda doubt it.
	    warning "GDB couldn't read file"
            exp_internal 0
	    return [gdb_stub_retry_ld "$dest" "$prog"]
	}
	timeout {
	    warning "(timeout) read symbol file"
	    verbose -log "\n\nAPB$expect_out(buffer)APB\n\n"
            exp_internal 0
	    return [gdb_stub_retry_ld "$dest" "$prog"]
	}
    }

    exp_internal 0

    # just in case there are old breakpoints lying around.
    gdb_stub_delete_breakpoints

    # We are more generic than upstream - we find out the protocol
    if {[board_info $dest exists gdb_protocol]} {
	set protocol [board_info $dest gdb_protocol]
    } else {
	set protocol "remote"
    }

    if {[board_info $dest exists gdb_serial]} {
	set serial [board_info $dest gdb_serial]
    } elseif {[board_info $dest exists serial]} {
	set serial [board_info $dest serial]
    } else {
	set serial [board_info $dest netport]
    }

    # technically, we only need to call this on the first program that is run
    # in the gdb session, but I don't yet know how to tweak the .exp for that
    remote_send host "target $protocol $serial\n"
    remote_expect host 60 {
	-re "Kill it?.*y or n.*" {
            # send no because we are going to call monitor reset next anyway
	    remote_send host "n\n"
	    exp_continue
	}
	-re "$gdb_prompt $"	{
	    verbose "Set remote target to $serial" 2
	}
	timeout {
	    warning "Couldn't set remote target."
	    return 0
	}
    }

    # Reset the target to get it into a consistent state.
    remote_send host "monitor reset\n"
    remote_expect host 10 {
	-re ".*$gdb_prompt $" {
	    verbose "Reset target" 2
	}
	default {
	    return { "fail" "" }
	}
    }


    # Set a remote CPU timeout in seconds if specified. This is an arbitrary
    # value.
    if {[target_info exists gdb,remote_timeout]} {
	remote_send host "monitor timeout [target_info gdb,remote_timeout]\n"
	remote_expect host 10 {
	    -re ".*$gdb_prompt $" { }
	    default {
		return { "fail" "" }
	    }
	}
    }

    if {[board_info $dest exists gdb_load_offset]} {
	set offset "[board_info $dest gdb_load_offset]"
    } else {
	set offset ""
    }
    remote_send host "load\n"
    verbose "Loading $prog into $GDB" 2
    global verbose
    remote_expect host 1200 {
	-re "Loading.*$gdb_prompt $" {
	    verbose "Loaded $prog into $GDB" 1
	}
	-re "$gdb_prompt $"     {
	    if $verbose>1 then {
		warning "GDB couldn't load."
	    }
	}
	timeout {
	    if $verbose>1 then {
		perror "Timed out trying to load $prog."
	    }
	}
    }
    return 1
}


# Override standard proc to deal with copyloop

# The standard proc does not allow for a question about deferring a breakpoint
# pending a future load.
proc gdb_stub_start { dest } {
    global gdb_prompt

    set exit_brnum [gdb_stub_add_breakpoint _exit]
    if { $exit_brnum == "undef" || [board_info $dest exists always_break_exit] } {
	set exit_brnum [gdb_stub_add_breakpoint exit]
    }
    set abort_brnum [gdb_stub_add_breakpoint abort]

    upvar #0 gdb_stub_info I
    set I($dest,exit_brnum) $exit_brnum
    set I($dest,abort_brnum) $abort_brnum

    remote_send host "set \$FP=0\n"
    remote_expect host 10 {
	-re "$gdb_prompt" { }
    }
    # This is needed for the SparcLite. Whee.
    if {[board_info $dest exists gdb,start_symbol]} {
	set start_comm "jump *[board_info $dest gdb,start_symbol]\n"
    } else {
	set start_comm "jump *start\n"
    }
    remote_send host "break copyloop\n"
    remote_expect host 10 {
	-re "Breakpoint.*$gdb_prompt $" {
	    set start_comm "continue\n"
	}
	# Extra possibility, not in the standard proc
	-re "Function.*not defined.*y or .*n.* $" {
	    remote_send host "n\n" }
	-re "Function.*not defined.*$gdb_prompt $" { }
	default { }
    }
    remote_send host $start_comm
    remote_expect host 10 {
	-re "y or n. $" {
	    remote_send host "y\n"
	    exp_continue
	}
	-re "Breakpoint.*in copyloop.*$gdb_prompt $" {
	    remote_send host "jump relocd\n"
	    exp_continue
	}
	-re "Continuing at\[^\r\n\]+\[\r\n\]" { }
	default {
	    return { "fail" "" }
	}
    }
    return { "pass" "" }
}

# Override standard function
proc gdb_stub_add_breakpoint { function args } {
    global gdb_prompt

    remote_send host "break $function\n"
    remote_expect host 60 {
	-re ".*reakpoint (\[0-9\]+).*$gdb_prompt $" { return $expect_out(1,string) }
	-re "Function.*not defined.*y or .*n.* $" {
	    remote_send host "n\n"
	    return "undef" }
	-re "Function.*not defined.*$gdb_prompt $" { return "undef" }
	-re "No symbol table.*$gdb_prompt $" { return "undef" }
	default {
	    return "undef"
	}
    }
}


# Override to be more picky about exits with non-zero return codes. Surely
# these should always be a fail?

# Return code is 0 for PASS, anything positive for FAIL, -2 for timeout (our
# extension) and anything else negative for "please retry". However we follow
# the upstream convention of just using 1 for fail, irrespective of the exit
# return code.
proc gdb_stub_wait { dest timeout } {
    global gdb_prompt
    global gdb_print_return_code

    # For some reason the breakpoint numbers don't always seem to be set. The
    # whole use of upvar seems a bit broken. So we catch and set defaults if
    # necessary.
    upvar #0 gdb_stub_info I
    if { [catch {set exit_brnum $I($dest,exit_brnum)}] } {
	send_user "Failed to find exit break number\n"
	set exit_brnum -1
    }
    if { [catch {set abort_brnum $I($dest,abort_brnum)}] } {
	send_user "Failed to find abort break number\n"
	set abort_brnum -1
    }

    remote_expect host $timeout {
	-re "Breakpoint\[^\n\]*exit.*=0.*$gdb_prompt $" {
	    # It is important that \[^\n\]* is included in the above regexp.
	    # When a breakpoint is hit, gdb includes the current source
	    # line in the output. If that source line contained 'exit', then
	    # it would cause this case to be triggered.

	    # Whether we pass or fail depends on the argument. Drop through,
	    # so we can find it.
	}
	-re "Breakpoint $exit_brnum.*$gdb_prompt $" {
	    # Whether we pass or fail depends on the argument. Drop through,
	    # so we can find it.
	}
	-re "Breakpoint\[^\n\]*exit.*$gdb_prompt $" {
	    # It is important that \[^\n\]* is included in the above regexp.
	    # When a breakpoint is hit, gdb includes the current source
	    # line in the output. If that source line contained 'exit', then
	    # it would cause this case to be triggered.
	    #

	    # Whether we pass or fail depends on the argument. Drop through,
	    # so we can find it.
	}
	-re "Breakpoint $abort_brnum.*$gdb_prompt $" {
	    gdb_stub_go_idle $dest
	    return [list 1 ""]
	}
	-re "Breakpoint\[^\n\]*abort.*$gdb_prompt $" {
            # We include \[^\n\]* in the above regexp, so that we don't match
            # an occurence of 'abort' printed in the source line when a
            # breakpoint is hit.

	    gdb_stub_go_idle $dest
	    return [list 1 ""]
	}
        -re " EXIT code 0.*$gdb_prompt $" {
            gdb_stub_go_idle $dest
            return [list 0 ""]
        }
        -re " EXIT code \[1-9]\[0-9]*.*$gdb_prompt $" {
	    # Surely this should be a fail? Change from the upstream
            gdb_stub_go_idle $dest
            return [list 1 ""]
        }
        -re " EXIT code 4242.*$gdb_prompt $" {
            gdb_stub_go_idle $dest
            return [list 1 ""]
        }
	-re "CPU time limit exceeded.*$gdb_prompt $" {
	    # This is a clean timeout, so should lead to UNRESOLVED, not
	    # FAIL. Must go before the generic "Program received" which
	    # catches any signal.
	    gdb_stub_go_idle $dest
	    return [list -2 ""]
	}
 	-re "Program received.*$gdb_prompt $" {
	    gdb_stub_go_idle $dest
	    return [list 1 ""]
	}
	-re "Program exited.*$gdb_prompt $" {
	    gdb_stub_go_idle $dest
	    return [list 1 ""]
	}
	-re "Remote connection closed.*$gdb_prompt $" {
	    remote_close $dest
	    remote_reboot $dest
	    return [list -1 ""]
	}
	timeout {
	    # This is a timeout, but not cleanly from the far end. So should
	    # lead to UNRESOLVED, not FAIL, but we need close and reboot the
	    # remote.
	    remote_close $dest
	    remote_reboot $dest
	    return [list -2 ""]
	}
	default {
	    verbose -log "Rebooting reason 3: unmatched output, default case\n"
	    remote_close $dest
	    remote_reboot $dest
	    return [list -1 ""]
	}
    }
    # If we drop out here, it's because we need to find out the return code.
    # Whether we pass or fail depends on the argument. This is target
    # specific.
    remote_send host "$gdb_print_return_code\n"
    remote_expect host $timeout {
	-re "\\\$\[0-9\]* = 0.*$gdb_prompt $" {
	    gdb_stub_go_idle $dest
	    return [list 0 ""]
	}
	-re "\\\$\[0-9\]* = \[1-9\]\[0-9\]*.*$gdb_prompt $" {
	    gdb_stub_go_idle $dest
	    return [list 1 ""]
	}
	-re "Remote connection closed.*$gdb_prompt $" {
	    remote_close $dest
	    remote_reboot $dest
	    return [list -1 ""]
	}
	default {
	    remote_close $dest
	    remote_reboot $dest
	    return [list -1 ""]
	}
    }

    return [list -1 ""]
}


# Load and run a program. The standard version is broken. In this version we
# pick up the correct timeout and also deal with a timeout result.
proc gdb_stub_load { dest prog args } {
    global gdb_prompt
    set argnames { "command-line arguments" "input file" "output file" }

    for { set x 0 } { $x < [llength $args] } { incr x } {
	if { [lindex $args $x] != "" } {
	    return [list "unsupported"
		    "no support for [lindex $argnames $x] on this target"]
	}
    }

    set result [remote_spawn $dest $prog]

    if { $result < 0 } {
	return [list "fail" "remote_spawn failed"]
    }

    # Get a wait timeout if we have one, else use the default value. Note that
    # the wait timeout must be significantly longer than the remote timeout,
    # to give the remote timeout enough time to happen.
    set wait_timeout 120
    if {[board_info $dest exists gdb,remote_timeout]} {
	set wait_timeout [expr 2 * [board_info $dest gdb,remote_timeout]]
    }

    set result [remote_wait $dest $wait_timeout]
    set status [lindex $result 0]
    set output [lindex $result 1]

    if { $status == 0 } {
	return [list "pass" $output]
    } elseif { $status > 0 } {
	return [list "fail" $output]
    } elseif { $status == -2 } {
	# Timed out
	return [list "unresolved" $output]
    } else {
	global gdb_stub_retry

	if {![info exists gdb_stub_retry]} {
	    set gdb_stub_retry 1

	    set result [eval gdb_stub_load \{$dest\} \{$prog\} $args]
	    unset gdb_stub_retry
	    return $result
	} else {
	    return [list "fail" $output]
	}
    }
}
