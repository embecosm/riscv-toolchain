# Board specification for the RISC-V GDB server

# Copyright (C) 2018 Embecosm Limited.

# This program is free software; you can redistribute it and/or modify it
# under the terms of the GNU General Public License as published by the Free
# Software Foundation; either version 3 of the License, or (at your option)
# any later version.

# This program is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
# FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for
# more details.

# You should have received a copy of the GNU General Public License along
# with this program.  If not, see <http://www.gnu.org/licenses/>.

# Delete these functions.
rename gdbserver_gdb_load {}
rename gdb_reload {}
rename gdb_reconnect {}
rename gdb_start {}
rename mi_gdb_target_cmd {}

proc gdb_reload_start_target { } {
    global gdb_prompt
    global GDBSERVER

    set targetname "remote"
    send_gdb "target remote | $GDBSERVER\n"
    gdb_expect 60 {
        -re "A program is being debugged already.*ill it.*y or n. $" {
            send_gdb "y\n"
            exp_continue
        }
        -re "Couldn't establish connection to remote.*$gdb_prompt $" {
            verbose "Connection failed"
        }
        -re "Remote MIPS debugging.*$gdb_prompt" {
            verbose "Set target to $targetname"
            return 0
        }
        -re "Remote debugging using .*-gdbserver.*$gdb_prompt $" {
            verbose "Set target to $targetname"
            return 0
        }
        -re "Remote target $targetname connected to.*$gdb_prompt $" {
            verbose "Set target to $targetname"
            return 0
        }
        -re "Connected to.*$gdb_prompt $" {
            verbose "Set target to $targetname"
            return 0
        }
        -re "Ending remote.*$gdb_prompt $" { }
        -re "Connection refused.*$gdb_prompt $" {
            verbose "Connection refused by remote target.  Pausing, and trying again."
            sleep 30
            continue
        }
        -re "Timeout reading from remote system.*$gdb_prompt $" {
            verbose "Got timeout error from gdb."
        }
        -notransfer -re "Remote debugging using .*\r\n> $" {
            # We got an unexpected prompt while creating the target.
            # Leave it there for the test to diagnose.
            return 1
        }
        timeout {
            send_gdb ""
            break
        }
    }

    return 1
}

proc gdb_reload { } {
    global gdb_prompt

    gdb_reload_start_target
    gdb_load_cmd
}

proc gdb_start { } {
    global gdb_prompt

    if [default_gdb_start] {
        return -1
    }

    # force the width to "unlimited", so no wraparound occurs
    send_gdb "set remote hardware-watchpoint-limit 0\n"
    gdb_expect 10 {
	-re "$gdb_prompt $" {
	    verbose "Setting hardware-watchpoint-limit to 0" 2
	}
	timeout {
	    warning "Couldn't set the hardware-watchpoint-limit to 0."
            return -1
	}
    }

    return 0
}

proc mi_gdb_target_cmd { protocol options } {
    global mi_gdb_prompt
    global GDBSERVER

    if [target_info exists gdb_load_timeout] {
	set loadtimeout [target_info gdb_load_timeout]
    } else {
	set loadtimeout 1600
    }

    send_gdb "47-target-select remote | ${GDBSERVER}\n"
    gdb_expect 100 {
        -re "47\\^connected.*$mi_gdb_prompt" {
        }
        timeout {
            perror "Unable to connect to remote target (timeout)"
            return -1
        }
    }

    send_gdb "48-gdb-set remote hardware-watchpoint-limit 0\n"
    gdb_expect 100 {
        -re "48\\^done.*$mi_gdb_prompt" {
        }
        timeout {
            perror "Unable to set hardware-watchpoint-limit to 0."
            return -1
        }
    }

    return 0
}
